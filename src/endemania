#!/bin/sh
exec ttr "$0" -- "$@"

if [ "$#" -eq 0 ]; then
    cat > $WORKING_DIR/input.txt

    if grep -E '(%[0-9a-fA-F][0-9a-fA-F]%[0-9a-fA-F][0-9a-fA-F]|^%[0-9a-fA-F][0-9a-fA-F]$)' $WORKING_DIR/input.txt >/dev/null; then
        env echo -e '\e[34murl-decode\e[0m'
        cat $WORKING_DIR/input.txt | perl $WORKING_DIR/main.pl -q --each-line url-decode
        exit
    fi

    if :; then
        env echo -e '\e[34murl-encode\e[0m'
        cat $WORKING_DIR/input.txt | perl $WORKING_DIR/main.pl -q --each-line url-encode
    fi

    if grep '%[0-9a-fA-F][0-9a-fA-F]' $WORKING_DIR/input.txt >/dev/null; then
        env echo -e '\e[34murl-decode\e[0m'
        cat $WORKING_DIR/input.txt | perl $WORKING_DIR/main.pl -q --each-line url-decode
    fi

    if :; then
        env echo -e '\e[34mbase64-encode\e[0m'
        cat $WORKING_DIR/input.txt | perl $WORKING_DIR/main.pl -q --each-line base64-encode
    fi

    if ! grep '[^0-9a-zA-Z+/=\n]' $WORKING_DIR/input.txt >/dev/null; then
        env echo -e '\e[34mbase64-decode\e[0m'
        cat $WORKING_DIR/input.txt | perl $WORKING_DIR/main.pl -q --each-line base64-decode
    fi

    if :; then
        env echo -e '\e[34municode-escape\e[0m'
        cat $WORKING_DIR/input.txt | perl $WORKING_DIR/main.pl -q --each-line unicode-escape
    fi

    if grep '\\u' $WORKING_DIR/input.txt >/dev/null; then
        env echo -e '\e[34municode-unescape\e[0m'
        cat $WORKING_DIR/input.txt | perl $WORKING_DIR/main.pl -q --each-line unicode-unescape
    fi

    if :; then
        env echo -e '\e[34municode-dump\e[0m'
        cat $WORKING_DIR/input.txt | perl $WORKING_DIR/main.pl -q --each-line unicode-dump
    fi

    if :; then
        env echo -e '\e[34mhex-dump\e[0m'
        cat $WORKING_DIR/input.txt | perl $WORKING_DIR/main.pl -q --each-line hex-dump
    fi

    if :; then
        env echo -e '\e[34mnfkc (互換合成)\e[0m'
        cat $WORKING_DIR/input.txt | perl $WORKING_DIR/main.pl -q --each-line nfkc
        cat $WORKING_DIR/input.txt | perl $WORKING_DIR/main.pl -q --each-line nfkc unicode-dump
        env echo -e '\e[34mnfkd (互換分解)\e[0m'
        cat $WORKING_DIR/input.txt | perl $WORKING_DIR/main.pl -q --each-line nfkd
        cat $WORKING_DIR/input.txt | perl $WORKING_DIR/main.pl -q --each-line nfkd unicode-dump
        env echo -e '\e[34mnfc (正規合成)\e[0m'
        cat $WORKING_DIR/input.txt | perl $WORKING_DIR/main.pl -q --each-line nfc
        cat $WORKING_DIR/input.txt | perl $WORKING_DIR/main.pl -q --each-line nfc unicode-dump
        env echo -e '\e[34mnfd (正規分解)\e[0m'
        cat $WORKING_DIR/input.txt | perl $WORKING_DIR/main.pl -q --each-line nfd
        cat $WORKING_DIR/input.txt | perl $WORKING_DIR/main.pl -q --each-line nfd unicode-dump
    fi

    if :; then
        env echo -e '\e[34mcharname\e[0m'
        cat $WORKING_DIR/input.txt | perl $WORKING_DIR/main.pl -q --each-line charname
    fi
else
    perl $WORKING_DIR/main.pl "$@"
fi

##file help.txt <<EOF

    Usage: endemania [options] [commands]

    option:
      --help
      --each-line
      -s string
      -q

    command:
      url-encode | urlencode

      url-decode | urldecode

      base64-encode
              Encode into base64 encoding. Encoded string is broken into lines of no
              more than 76 characters each

      base64-long-encode
              Encode into base64 encoding. Encoded string is not broken into lines.

      base64-decode

      unicode-encode | unicode-escape

      unicode-decode | unicode-unescape

      *-char-encode | *-char-encode-perl

      *-char-decode | *-char-decode-perl

      utf8-encode
              alias to utf-8-char-encode-perl

      utf8-decode
              alias to utf-8-char-decode-perl

      utf16-encode
              alias to utf-16-be-char-encode-perl

      utf16-decode
              alias to utf-16-be-char-decode-perl

      unicode-dump

      hex-dump

      hex-undump

      bin-dump

      bin-undump

      nfkc
      nfkd
      nfc
      nfd

      charname

      idna-encode
      idna-decode

      python-unicode-escape
      python-unicode-unescape
      python-raw-unicode-escape
      python-raw-unicode-unescape

    encoding:
      utf8
      utf16
      iso-8859-1
      sjis
      eucjp

EOF

##file main.pl <<EOF

    use strict;
    use warnings;
    use utf8;

    use Encode qw/decode encode decode_utf8 encode_utf8/;
    use MIME::Base64 qw/encode_base64 decode_base64/;
    use Unicode::Normalize qw/NFKC NFKD NFC NFD/;

    use IPC::Open2 qw/open2/;

    my $default_encoding = 'utf-8';

    sub pipe_perl_char_encode {
        my ($charset) = @_;
        (sub {
            my ($line) = @_;
            my $result = encode($charset, $line);
            $result;
        }, 'str', 'bin', "charset-char-encode-perl");
    }

    sub pipe_perl_char_decode {
        my ($charset) = @_;
        (sub {
            my ($line) = @_;
            my $result = decode($charset, $line);
            $result;
        }, 'bin', 'str', "charset-char-decode-perl");
    }

    sub pipe_url_encode {
        (sub {
            my ($line) = @_;
            my $result = $line;
            $result =~ s/([^ 0-9a-zA-Z])/"%".uc(unpack("H2",$1))/eg;
            $result =~ s/ /+/g;
            $result;
        }, 'bin', 'str', "url-encode");
    }

    sub pipe_url_decode {
        (sub {
            my ($line) = @_;
            my $result = $line;
            $result =~ s/\+/ /g;
            $result =~ s/%([0-9a-fA-F]{2})/pack("H2",$1)/eg;
            $result = encode("iso-8859-1", $result);
            $result;
        }, 'str', 'bin', "url-decode");
    }

    sub pipe_base64_encode {
        (sub {
            my ($line) = @_;
            my $result = encode_base64($line);
            $result;
        }, 'bin', 'str', "base64-encode");
    }

    sub pipe_base64_long_encode {
        (sub {
            my ($line) = @_;
            my $result = encode_base64($line, '');
            $result;
        }, 'bin', 'str', "base64-long-encode");
    }

    sub pipe_base64_decode {
        (sub {
            my ($line) = @_;
            my $result = decode_base64($line);
            $result;
        }, 'str', 'bin', "base64-decode");
    }

    sub pipe_unicode_escape {
        my $p1 = sub {
            my ($y) = @_;
            '\\' x (length($y) * 2) . 'u';
        };
        my $p2 = sub {
            my ($ch) = @_;
            my $c = ord($ch);
            if ($c <= 0x7E) {
                return $ch;
            }
            if ($c >= 0x10000) {
                my $c1 = ($c - 0x10000) / 0x400 + 0xD800;
                my $c2 = ($c - 0x10000) % 0x400 + 0xDC00;
                sprintf("\\u%04x\\u%04x", $c1, $c2);
            } else {
                sprintf("\\u%04x", $c);
            }
        };
        (sub {
            my ($line) = @_;
            $line =~ s/(\\+)u/$p1->($1)/eg;
            $line =~ s/(.)/$p2->($1)/eg;
            $line;
         }, 'str', 'str', "unicode-escape");
    }

    sub pipe_unicode_unescape {
        my $p1 = sub {
            my ($y, $hex) = @_;
            my $len = length($y);
            if ($len % 2 == 0) {
                return '\\' x ($len / 2) . 'u' . $hex;
            } else {
                return '\\' x (($len - 1) / 2) . chr(hex($hex));
            }
        };
        my $p2 = sub {
            my ($ch1, $ch2) = @_;
            my $c = (ord($ch1) - 0xD800) * 0x400 + (ord($ch2) - 0xDC00) + 0x10000;
            chr($c);
        };
        (sub {
            my ($line) = @_;
            $line =~ s/(\\+)u+([0-9a-fA-F]{4})/$p1->($1, $2)/eg;
            $line =~ s/([\x{D800}-\x{DBFF}])([\x{DC00}-\x{DFFF}])/$p2->($1, $2)/eg;
            $line;
         }, 'str', 'str', "unicode-unescape");
    }

    sub pipe_unicode_dump {
        my $p = sub {
            my ($ch) = @_;
            my $c = ord($ch);
            sprintf("%04X ", $c);
        };
        (sub {
            my ($line) = @_;
            $line =~ s/([^\n])/$p->($1)/eg;
            $line;
         }, 'str', 'str', "unicode-dump");
    }

    sub pipe_hex_dump {
        my $p = sub {
            my ($ch) = @_;
            my $c = ord($ch);
            sprintf("%02X ", $c);
        };
        (sub {
            my ($line) = @_;
            $line =~ s/([^\n])/$p->($1)/eg;
            $line;
         }, 'bin', 'str', "hex-dump");
    }

    sub pipe_hex_undump {
        my $p = sub {
            my ($ch) = @_;
            pack("H2", $ch);
        };
        (sub {
            my ($line) = @_;
            $line =~ s/([0-9a-fA-F]{2})\s*/$p->($1)/eg;
            $line = encode("iso-8859-1", $line);
            $line;
         }, 'str', 'bin', "hex-undump");
    }

    sub pipe_bin_dump {
        my $p = sub {
            my ($ch) = @_;
            my $c = ord($ch);
            sprintf("%08B ", $c);
        };
        (sub {
            my ($line) = @_;
            $line =~ s/([^\n])/$p->($1)/eg;
            $line;
         }, 'bin', 'str', "bin-dump");
    }

    sub pipe_bin_undump {
        my $p = sub {
            my ($ch) = @_;
            pack("B8", $ch);
        };
        (sub {
            my ($line) = @_;
            $line =~ s/([01]{8})\s*/$p->($1)/eg;
            $line = encode("iso-8859-1", $line);
            $line;
         }, 'str', 'bin', "bin-undump");
    }

    sub pipe_nfkc {
        (sub {
            my ($line) = @_;
            NFKC($line);
         }, 'str', 'str', "nfkc");
    }

    sub pipe_nfkd {
        (sub {
            my ($line) = @_;
            NFKD($line);
         }, 'str', 'str', "nfkd");
    }

    sub pipe_nfc {
        (sub {
            my ($line) = @_;
            NFC($line);
         }, 'str', 'str', "nfc");
    }

    sub pipe_nfd {
        (sub {
            my ($line) = @_;
            NFD($line);
         }, 'str', 'str', "nfd");
    }

    my $python_pid = '';

    sub start_python {
        unless ($python_pid) {
            $python_pid = open2(*PY_READ, *PY_WRITE, "python3 -u $ENV{WORKING_DIR}/sub.py");
        }
    }

    sub end_python {
        if ($python_pid) {
            close(PY_WRITE);
            close(PY_READ);
        }
    }

    sub pipe_python {
        my ($op, $name) = @_;
        start_python();
        (sub {
            my ($line) = @_;
            $line = encode_utf8($line);
            print PY_WRITE "$op$line\n";
            my $result = <PY_READ>;
            $result = decode_utf8($result);
            $result;
        }, 'str', 'str', $name);
    }

    sub pipe_charname {
        pipe_python("u", "charname");
    }

    sub pipe_idna_encode {
        pipe_python("d", "idna-encode");
    }

    sub pipe_idna_decode {
        pipe_python("D", "idna-decode");
    }

    sub pipe_python_unicode_escape {
        pipe_python("s", "python-unicode-escape");
    }

    sub pipe_python_unicode_unescape {
        pipe_python("S", "python-unicode-unescape");
    }

    sub pipe_python_raw_unicode_escape {
        pipe_python("r", "python-raw-unicode-escape");
    }

    sub pipe_python_raw_unicode_unescape {
        pipe_python("R", "python-raw-unicode-unescape");
    }

    my $is_help = '';
    my $is_each_line = '';
    my $is_verbose = 1;
    my @pipesInfo = ();
    my @lines = ();

    while () {
        last if (@ARGV == 0);
        my $a = shift;
        my @pipeInfo = ();
        if ($a eq '-s') {
            last if (@ARGV == 0);
            my $b = shift;
            push(@lines, $b);
        } elsif ($a eq '--help') {
            $is_help = 1;
        } elsif ($a eq '--each-line') {
            $is_each_line = 1;
        } elsif ($a eq '-q') {
            $is_verbose = '';

        } elsif ($a eq 'url-encode' || $a eq 'urlencode') {
            @pipeInfo = pipe_url_encode;
        } elsif ($a eq 'url-decode' || $a eq 'urldecode') {
            @pipeInfo = pipe_url_decode;

        } elsif ($a eq 'base64-encode') {
            @pipeInfo = pipe_base64_encode;
        } elsif ($a eq 'base64-long-encode') {
            @pipeInfo = pipe_base64_long_encode;
        } elsif ($a eq 'base64-decode') {
            @pipeInfo = pipe_base64_decode;

        } elsif ($a eq 'unicode-encode' || $a eq 'unicode-escape') {
            @pipeInfo = pipe_unicode_escape();
        } elsif ($a eq 'unicode-decode' || $a eq 'unicode-unescape') {
            @pipeInfo = pipe_unicode_unescape();

        } elsif ($a =~ /\A(.+)-char-encode\Z/) {
            @pipeInfo = pipe_perl_char_encode($1);
        } elsif ($a =~ /\A(.+)-char-decode\Z/) {
            @pipeInfo = pipe_perl_char_decode($1);

        } elsif ($a =~ /\A(.+)-char-encode-perl\Z/) {
            @pipeInfo = pipe_perl_char_encode($1);
        } elsif ($a =~ /\A(.+)-char-decode-perl\Z/) {
            @pipeInfo = pipe_perl_char_decode($1);

        } elsif ($a eq 'utf8-encode') {
            @pipeInfo = pipe_perl_char_encode('utf-8');
        } elsif ($a eq 'utf8-decode') {
            @pipeInfo = pipe_perl_char_decode('utf-8');

        } elsif ($a eq 'utf16-encode') {
            @pipeInfo = pipe_perl_char_encode('utf-16-be');
        } elsif ($a eq 'utf16-decode') {
            @pipeInfo = pipe_perl_char_decode('utf-16-be');

        } elsif ($a eq 'unicode-dump') {
            @pipeInfo = pipe_unicode_dump();

        } elsif ($a eq 'hex-dump') {
            @pipeInfo = pipe_hex_dump();
        } elsif ($a eq 'hex-undump') {
            @pipeInfo = pipe_hex_undump();

        } elsif ($a eq 'bin-dump') {
            @pipeInfo = pipe_bin_dump();
        } elsif ($a eq 'bin-undump') {
            @pipeInfo = pipe_bin_undump();

        } elsif ($a eq 'nfkc') {
            @pipeInfo = pipe_nfkc();
        } elsif ($a eq 'nfkd') {
            @pipeInfo = pipe_nfkd();
        } elsif ($a eq 'nfc') {
            @pipeInfo = pipe_nfc();
        } elsif ($a eq 'nfd') {
            @pipeInfo = pipe_nfd();

        } elsif ($a eq 'charname') {
            @pipeInfo = pipe_charname();

        } elsif ($a eq 'idna-encode') {
            @pipeInfo = pipe_idna_encode();
        } elsif ($a eq 'idna-decode') {
            @pipeInfo = pipe_idna_decode();

        } elsif ($a eq 'python-unicode-escape') {
            @pipeInfo = pipe_python_unicode_escape();
        } elsif ($a eq 'python-unicode-unescape') {
            @pipeInfo = pipe_python_unicode_unescape();
        } elsif ($a eq 'python-raw-unicode-escape') {
            @pipeInfo = pipe_python_raw_unicode_escape();
        } elsif ($a eq 'python-raw-unicode-unescape') {
            @pipeInfo = pipe_python_raw_unicode_unescape();

        } else {
            die "Illegal command: $a\n";
        }
        if (@pipeInfo) {
            push(@pipesInfo, \@pipeInfo);
        }
    }

    sub help {
        print STDERR "help";
    }

    if ($is_help) {
        help();
        exit;
    }

    sub createPipes {
        print "\x1b[34mBIN" if ($is_verbose);
        my @pipes = ();
        my $prev_type = 'bin';
        foreach my $p (@pipesInfo) {
            my ($pipe, $in_type, $out_type, $name) = @$p;
            if ($prev_type eq 'bin' && $in_type eq 'str') {
                my ($p) = pipe_perl_char_decode($default_encoding);
                push(@pipes, $p);
                print " =[$default_encoding-char-decode]=> STR" if ($is_verbose);
            } elsif ($prev_type eq 'str' && $in_type eq 'bin') {
                my ($p) = pipe_perl_char_encode($default_encoding);
                push(@pipes, $p);
                print " =[$default_encoding-char-encode]=> BIN" if ($is_verbose);
            }
            $prev_type = $out_type;
            push(@pipes, $pipe);
            if ($is_verbose) {
                print " =[$name]=> ";
                if ($prev_type eq 'str') {
                    print "STR";
                } elsif ($prev_type eq 'bin') {
                    print "BIN";
                }
            }
        }
        if ($prev_type eq 'str') {
            my ($p) = pipe_perl_char_encode($default_encoding);
            push(@pipes, $p);
            print " =[$default_encoding-char-encode]=> BIN" if ($is_verbose);
        }
        print "\x1b[0m\n" if ($is_verbose);
        @pipes;
    }

    my @pipes = createPipes();

    if (@lines) {
        foreach my $line (@lines) {
            foreach my $p (@pipes) {
                $line = $p->($line);
            }
            print $line;
            print "\n";
        }
    } elsif ($is_each_line) {
        while (my $line = <STDIN>) {
            #my $exists_lf = '';
            my $exists_lf = 1;
            my $exists_lf2 = '';
            if ($line =~ /\A(.*)\n\Z/) {
                $line = $1;
                $exists_lf = 1;
            }
            foreach my $p (@pipes) {
                $line = $p->($line);
            }
            if ($line =~ /\A(.*)\n\Z/) {
                $exists_lf2 = 1;
            }
            print $line;
            if ($exists_lf && !$exists_lf2) {
                print "\n";
            }
        }
    } else {
        my @lines = <STDIN>;
        my $line = join('', @lines);
        $line = $line;
        foreach my $p (@pipes) {
            $line = $p->($line);
        }
        print $line;
    }

    end_python();

EOF

##file sub.py <<EOF

    import sys
    import unicodedata

    for line in sys.stdin:
        op = line[0]
        line = line[1:-1]
        try:
            if op == 'u':
                result = ""
                for ch in line:
                    try:
                        result = result + "{" + ("%04X" % ord(ch)) + "/" + unicodedata.name(ch) + "}"
                    except:
                        result = result + "{" + ("%04X" % ord(ch)) + "}"
                sys.stdout.write(result)
            elif op == 'd':
                sys.stdout.write(line.encode("idna").decode("utf-8"))
            elif op == 'D':
                sys.stdout.write(line.encode("utf-8").decode("idna"))
            elif op == 's':
                sys.stdout.write(line.encode("unicode_escape").decode("utf-8"))
            elif op == 'S':
                sys.stdout.write(line.encode("utf-8").decode("unicode_escape"))
            elif op == 'r':
                sys.stdout.write(line.encode("raw_unicode_escape").decode("utf-8"))
            elif op == 'R':
                sys.stdout.write(line.encode("utf-8").decode("raw_unicode_escape"))
            else:
                sys.stdout.write("ERROR")
        except:
            raise
            #pass
        sys.stdout.write("\n")

EOF

